import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

group = 'com.drtshock'
archivesBaseName = 'FactionShock'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks 'build', 'shadowJar'

jar {
    classifier = 'original'
    // remove the version string from the final jar name
    version = null
}

shadowJar {
    classifier = ''
    // remove the version string from the final jar name
    version = null
}

shadowJar {
    relocate 'com.google.gson', 'com.drtshock.factionshock.vendor.com.google.gson'

    dependencies {
        include(dependency('com.google.code.gson:gson:.*'))
    }
}

// process resource tokens. the token will be @project.version@
processResources {
    filter ReplaceTokens, tokens: [
        'project.version': project.version
    ]
}

publishing {
    repositories {
        publications {
            shadow(MavenPublication) {
                from components.java
                artifact shadowJar
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name "sponge-repo"
        url "http://repo.spongepowered.org/maven"
    }

    maven {
        name "sk89q-repo"
        url "http://maven.sk89q.com/repo/"
    }
}

dependencies {
    compile 'org.spongepowered:spongeapi:1.1-SNAPSHOT'
    compile 'com.sk89q:worldguard:6.0.0-SNAPSHOT'
    compile 'com.sk89q:worldedit:6.0.0-SNAPSHOT'
    compile 'com.google.code.gson:gson:2.1'
    compile 'com.google.guava:guava:10.0.1'
}
