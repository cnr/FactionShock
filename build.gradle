
plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

group = 'com.drtshock'
version = '0.0.1-SNAPSHOT'

defaultTasks 'build'

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name "sponge-repo"
        url "http://repo.spongepowered.org/maven"
    }

    maven {
        name "sk89q-repo"
        url "http://maven.sk89q.com/repo/"
    }
}

dependencies {
    compile 'org.spongepowered:spongeapi:1.1-SNAPSHOT'
    compile 'com.sk89q:worldguard:6.0.0-SNAPSHOT'
    compile 'com.sk89q:worldedit:6.0.0-SNAPSHOT'
    compile 'com.google.code.gson:gson:2.1'
    compile 'com.google.guava:guava:10.0.1'
}

jar {
    classifier = 'original'
    version = null
}

shadowJar {
    classifier = ''
    version = null

    dependencies {
        include(dependency('com.google.code.gson:gson'))
    }

    relocate 'com.google.gson', 'com.drtshock.factionshock.vendor.com.google.gson'
}
build.dependsOn(shadowJar)

processResources {
    expand version: version
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifact shadowJar
        }
    }
}
